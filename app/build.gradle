plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdk 31

    defaultConfig {
        applicationId "ru.solom.magiclamp"
        minSdk 23
        targetSdk 31
        versionCode System.getenv("VERSION_CODE") ?: 1
        versionName System.getenv("VERSION_NAME") ?: "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            def keystorePropsFile = new File("$rootDir/keystore/config")

            storeFile = new File("$rootDir/keystore/keystore.jks")
            if (keystorePropsFile.exists()) {
                keystoreProperties.load(new FileInputStream(keystorePropsFile))
                storePassword "$keystoreProperties.keyStorePass"
                keyAlias "$keystoreProperties.keyAlias"
                keyPassword "$keystoreProperties.keyPass"
            } else {
                storePassword System.getenv('KEYSTORE_PASS')
                keyAlias System.getenv('KEY_ALIAS')
                keyPassword System.getenv('KEY_PASS')
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation deps.google.material

    implementation deps.androidx.core
    implementation deps.androidx.appcompat
    implementation deps.androidx.constraint_layout
    implementation deps.androidx.swipe_refresh_layout
    implementation deps.androidx.lifecycle_viewmodel
    implementation deps.androidx.fragment_ktx

    implementation deps.coroutines
    implementation deps.stdlib

    implementation deps.hilt
    kapt deps.hilt_kapt
}